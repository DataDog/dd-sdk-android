/*
 * Unless explicitly stated otherwise all files in this repository are licensed under the Apache License Version 2.0.
 * This product includes software developed at Datadog (https://www.datadoghq.com/).
 * Copyright 2016-Present Datadog, Inc.
 */

import com.datadog.gradle.Dependencies
import com.datadog.gradle.config.AndroidConfig
import com.datadog.gradle.config.configureFlavorForSampleApp
import com.datadog.gradle.config.dependencyUpdateConfig
import com.datadog.gradle.config.java17
import com.datadog.gradle.config.javadocConfig
import com.datadog.gradle.config.junitConfig
import com.datadog.gradle.config.kotlinConfig
import com.datadog.gradle.config.taskConfig
import com.datadog.gradle.plugin.InstrumentationMode

plugins {
    id("com.android.application")
    kotlin("android")
    alias(libs.plugins.composeCompilerPlugin)
    kotlin("kapt")
    id("com.github.ben-manes.versions")
    id("org.jetbrains.dokka-javadoc")
    id("com.squareup.sqldelight")
    id("com.google.devtools.ksp")
    alias(libs.plugins.datadogGradlePlugin)
}

sqldelight {
    database("LogsDatabase") {
        packageName = "com.datadog.android.sample"
        dialect = "sqlite:3.24"
        sourceFolders = listOf("sqldelight")
    }
}

@Suppress("StringLiteralDuplication")
android {
    compileSdk = AndroidConfig.TARGET_SDK
    buildToolsVersion = AndroidConfig.BUILD_TOOLS_VERSION

    defaultConfig {
        minSdk = AndroidConfig.MIN_SDK
        targetSdk = AndroidConfig.TARGET_SDK
        versionCode = AndroidConfig.VERSION.code
        versionName = AndroidConfig.VERSION.name
        multiDexEnabled = true

        buildFeatures {
            buildConfig = true
        }

        vectorDrawables.useSupportLibrary = true
        externalNativeBuild {
            cmake {
                cppFlags.add("-std=c++14")
            }
        }
    }

    namespace = "com.datadog.android.sample"

    compileOptions {
        isCoreLibraryDesugaringEnabled = true
        java17()
    }

    buildFeatures {
        compose = true
    }

    testOptions {
        unitTests.isReturnDefaultValues = true
    }

    flavorDimensions += listOf("site")
    productFlavors {
        val regions = arrayOf("us1", "us3", "us5", "us1_fed", "eu1", "ap1", "ap2", "staging")

        regions.forEachIndexed { index, region ->
            register(region) {
                isDefault = index == 0
                dimension = "site"
                configureFlavorForSampleApp(project, this, project.rootDir)
            }
        }
    }

    sourceSets.named("main") {
        java.srcDir("src/main/kotlin")
    }
    sourceSets.named("test") {
        java.srcDir("src/test/kotlin")
    }
    sourceSets.named("androidTest") {
        java.srcDir("src/androidTest/kotlin")
    }

    packaging {
        resources {
            excludes += "META-INF/*"
        }
    }

    externalNativeBuild {
        cmake {
            path = File("$projectDir/src/main/cpp/CMakeLists.txt")
            version = Dependencies.Versions.CMake
        }
    }
    ndkVersion = Dependencies.Versions.Ndk

    val e2ePassword = System.getenv("E2E_STORE_PASSWD")
    signingConfigs {
        if (e2ePassword != null) {
            create("release") {
                storeFile = File(project.rootDir, "sample-android.keystore")
                storePassword = e2ePassword
                keyAlias = "dd-sdk-android"
                keyPassword = e2ePassword
            }
        }
    }

    buildTypes {
        getByName("debug") {
            isMinifyEnabled = false
        }

        getByName("release") {
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
            isMinifyEnabled = false
            if (e2ePassword != null) {
                signingConfig = signingConfigs.getByName("release")
            } else {
                signingConfig = signingConfigs.getByName("debug")
            }
        }
    }
}

datadog {
    composeInstrumentation = InstrumentationMode.AUTO
}

dependencies {
    // Datadog Libraries
    implementation(project(":features:dd-sdk-android-logs"))
    implementation(project(":features:dd-sdk-android-flags"))
    implementation(project(":features:dd-sdk-android-rum"))
    debugImplementation(project(":features:dd-sdk-android-rum-debug-widget"))
    implementation(project(":features:dd-sdk-android-trace"))
    implementation(project(":features:dd-sdk-android-trace-otel"))
    implementation(project(":features:dd-sdk-android-ndk"))
    implementation(project(":features:dd-sdk-android-webview"))
    implementation(project(":features:dd-sdk-android-session-replay"))
    implementation(project(":features:dd-sdk-android-session-replay-material"))
    implementation(project(":features:dd-sdk-android-session-replay-compose"))
    implementation(project(":integrations:dd-sdk-android-trace-coroutines"))
    implementation(project(":integrations:dd-sdk-android-rum-coroutines"))
    implementation(project(":integrations:dd-sdk-android-rx"))
    implementation(project(":integrations:dd-sdk-android-timber"))
    implementation(project(":integrations:dd-sdk-android-coil"))
    implementation(project(":integrations:dd-sdk-android-coil3"))
    implementation(project(":integrations:dd-sdk-android-glide"))
    implementation(project(":integrations:dd-sdk-android-fresco"))
    implementation(project(":integrations:dd-sdk-android-sqldelight"))
    implementation(project(":integrations:dd-sdk-android-compose"))
    implementation(project(":integrations:dd-sdk-android-cronet"))
    implementation(project(":integrations:dd-sdk-android-okhttp"))
    implementation(project(":integrations:dd-sdk-android-okhttp-otel"))
    implementation(project(":tools:benchmark"))

    // Desugaring SDK
    coreLibraryDesugaring(libs.androidDesugaringSdk)

    // Sample Vendor Library
    implementation(project(":sample:vendor-lib"))

    implementation(libs.kotlin)

    // Android dependencies
    implementation(libs.androidXMultidex)
    implementation(libs.cronetPlayServices)
    implementation(libs.bundles.androidXNavigation)
    implementation(libs.androidXAppCompat)
    implementation(libs.bundles.androidXCompose)
    implementation(libs.material3Android)
    implementation(libs.googleAccompanistAppCompatTheme)
    implementation(libs.googleAccompanistPager)
    implementation(libs.googleAccompanistPagerIndicators)
    implementation(libs.googleMaterial)
    implementation(libs.androidx.navigation3.ui)
    implementation(libs.androidx.navigation3.runtime)
    implementation(libs.kotlinx.serialization.core)
    implementation(libs.androidx.lifecycle.viewmodel.navigation3)
    implementation("androidx.media:media:1.3.1")
    implementation("androidx.vectordrawable:vectordrawable:1.1.0")
    implementation("androidx.legacy:legacy-support-v4:1.0.0")
    implementation("androidx.lifecycle:lifecycle-extensions:2.2.0")
    implementation("androidx.preference:preference-ktx:1.1.1")

    // Image Loading Library
    implementation(libs.coil)
    implementation(libs.coilCompose)
    implementation(libs.coil3)
    implementation(libs.coil3Compose)
    implementation(libs.coil3NetworkOkHttp)
    implementation(libs.bundles.fresco)
    implementation(libs.bundles.glide)
    implementation(libs.picasso)
    kapt(libs.glideCompiler)

    // Local Storage
    implementation(libs.sqlDelight)
    implementation(libs.room)
    ksp(libs.roomCompiler)

    // Multithreading
    implementation(libs.rxJava3)
    implementation("com.squareup.retrofit2:adapter-rxjava3:2.9.0")
    implementation(libs.rxJava3Android)
    implementation(libs.coroutinesCore)

    // Network
    implementation("com.squareup.retrofit2:retrofit:2.9.0")
    implementation("com.squareup.retrofit2:converter-gson:2.9.0")
    implementation(libs.okHttp)
    implementation(libs.gson)
    implementation("com.launchdarkly:okhttp-eventsource:2.5.0")

    // Misc
    implementation(libs.timber)
    api("com.facebook.stetho:stetho:1.6.0")
}

kotlinConfig(evaluateWarningsAsErrors = false)
taskConfig<org.jetbrains.kotlin.gradle.tasks.KotlinCompile> {
    compilerOptions {
        optIn.add("kotlin.RequiresOptIn")
    }
}
junitConfig()
javadocConfig()
dependencyUpdateConfig()
