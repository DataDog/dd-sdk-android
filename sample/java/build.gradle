/*
 * Unless explicitly stated otherwise all files in this repository are licensed under the Apache License Version 2.0.
 * This product includes software developed at Datadog (https://www.datadoghq.com/).
 * Copyright 2016-2019 Datadog, Inc.
 */

apply plugin: "com.android.application"

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine "git", "rev-parse", "--short", "HEAD"
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 29
        versionCode = 1
        versionName = "1.0"
        applicationId = "com.datadog.android.sample"

        buildConfigField("String", "GIT_COMMIT_H", "\"${getGitHash()}\"")

        if (project.hasProperty("DD_CLIENT_TOKEN")) {
            buildConfigField("String", "DD_CLIENT_TOKEN", "\"${project.property("DD_CLIENT_TOKEN")}\"")
        } else {
            buildConfigField("String", "DD_CLIENT_TOKEN", "\"\"")
        }

        multiDexEnabled = true

        vectorDrawables.useSupportLibrary = true
    }

    // Specifies one flavor dimension.
    flavorDimensions "version"
    productFlavors {
        staging {
            dimension "version"
            buildConfigField("String", "DD_OVERRIDE_LOGS_URL", "\"${project.findProperty("DD_OVERRIDE_LOGS_URL")}\"")
            buildConfigField("String", "DD_OVERRIDE_TRACES_URL", "\"${project.findProperty("DD_OVERRIDE_TRACES_URL")}\"")
            buildConfigField("String", "DD_OVERRIDE_RUM_URL", "\"${project.findProperty("DD_OVERRIDE_RUM_URL")}\"")
            buildConfigField("String", "DD_RUM_APPLICATION_ID", "\"${project.findProperty("DD_RUM_APPLICATION_ID")}\"")
        }
        full {
            dimension "version"
            buildConfigField("String", "DD_OVERRIDE_LOGS_URL", "null")
            buildConfigField("String", "DD_OVERRIDE_TRACES_URL", "null")
            buildConfigField("String", "DD_OVERRIDE_RUM_URL", "null")
            buildConfigField("String", "DD_RUM_APPLICATION_ID", "null")
        }
    }
}

repositories {
    google()
    mavenLocal()
    mavenCentral()
}

dependencies {
    // Android dependencies
    implementation "androidx.multidex:multidex:2.0.1"
    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation "androidx.constraintlayout:constraintlayout:2.0.0-beta3"
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation "androidx.navigation:navigation-fragment:2.1.0"
    implementation "androidx.navigation:navigation-ui:2.1.0"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.1.0'

    implementation "com.google.code.gson:gson:2.8.6"

    // if you want to test the library in production you should change this dependency with the
    // latest release maven artifact either local or live
    // (e.g. "com.datadoghq:dd-sdk-android:1.0.0")
    implementation(project(":dd-sdk-android"))

    implementation "com.facebook.stetho:stetho:1.5.1"
    implementation "com.facebook.stetho:stetho-okhttp3:1.5.1"
}
